buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath gradlePlugins.kotlin
    }
}

allprojects{
    defaultTasks 'clean', 'build', 'jacocoTestReport'
}

subprojects{

    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    group = 'org.suggs.fsm'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j

        testImplementation libs.test.junitApi,
                libs.test.kotestAssertions,
                libs.test.mockito

        testRuntimeOnly libs.test.junitEngine,
                libs.logback
    }

    ext {
        sourceCompatibility = 1.14
        targetCompatibility = 1.14
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "14"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "14"
        }
    }
}
